buildscript {
    ext.kotlin_version = project.kotlin_version
    repositories {
        mavenCentral()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven { url = "http://repo.maven.apache.org/maven2" }
        maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${project.kotlin_version}"
    }
}
apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: 'kotlin'
apply plugin: "maven-publish"
apply plugin: "maven"

archivesBaseName = project.mod_name.replace(' ', '-').toLowerCase()
group = "net.ndrei.${project.mod_id}"

version = "${project.minecraft_version}-${project.mod_version}"
if (project.hasProperty('mod_suffix')) {
    version += "-${project.mod_suffix}"
}

compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
    version = "${minecraft_version}-${forge_version}"
    mappings = "${mcp_version}"
    runDir = "run"

    replace '@MOD-ID@', project.mod_id
    replace '@MOD-NAME@', project.mod_name
    replace '@MOD-VERSION@', project.mod_version
    replace '@MOD-MC-VERSION@', project.minecraft_version
    replace '@FORGELIN-VERSION@', project.forgelin_version
    replace '@FORGE-VERSION@', project.forge_version

    replaceIn 'constants.kt'
}

// Mad hacks to make source replacements work for Kotlin
// "stolen" from: https://github.com/PaleoCrafter/VanillaImmersion/blob/ee82ecafb76659cf7d7822a722c8f63f43f41d01/build.gradle#L119-L136
import net.minecraftforge.gradle.user.TaskSourceCopy
for (set in sourceSets) {
    def taskName = "source${set.name.capitalize()}Kotlin"
    def dir = new File(project.getBuildDir(), "sources/${set.name}/kotlin")
    task(taskName, type: TaskSourceCopy) {
        source = set.getKotlin()
        output = dir
    }
    def compileTask = tasks[set.getCompileTaskName("kotlin")]
    compileTask.source = dir
    compileTask.dependsOn taskName
    def dirPath = dir.toPath()
    compileKotlin.include {
        return it.file.toPath().startsWith(dirPath)
    }
}
// end of "stolen" part :)

configurations {
    compile.extendsFrom exportedCompile
    deployerJars
}

repositories {
    mavenCentral()
    jcenter()
    maven { url 'http://maven.epoxide.xyz' }
    maven { url 'http://maven.ndrei.net' }
    maven { url 'http://maven.shadowfacts.net' }
    maven { url 'http://maven.covers1624.net' }
    maven { url 'https://www.mod-buildcraft.com/maven' }
    mavenLocal()
}

dependencies {
    deployerJars 'org.apache.maven.wagon:wagon-ftp:2.2'
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compile "net.shadowfacts:Forgelin:$forgelin_version"

    compileOnly "net.darkhax.tesla:Tesla-1.12:$tesla_version:deobf"
    compileOnly "com.mod-buildcraft:buildcraft-api:$bc_version"
    compile "cofh:RedstoneFlux:1.12-$redstoneflux_version:universal"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    inputs.property "modid", project.mod_id
    inputs.property "modname", project.mod_name

    from(sourceSets.main.resources.srcDirs) {
        include '**/*.info'
        include '**/*.properties'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version, 'modid': project.mod_id, 'modname': project.mod_name
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.info'
        exclude '**/*.properties'
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

artifacts {
    archives sourceJar, deobfJar
}

if (project.hasProperty('mavenFTPServer') && project.hasProperty('mavenFTPUser') && project.hasProperty('mavenFTPPassword')) {
    uploadArchives {
        repositories.mavenDeployer {
            dependsOn build, deobfJar, sourcesJar

            configuration = configurations.deployerJars
            repository(url: project.mavenFTPServer) {
                authentication(userName: project.mavenFTPUser, password: mavenFTPPassword)
            }
        }
    }
    uploadArchives.dependsOn build, sourcesJar, deobfJar
}
